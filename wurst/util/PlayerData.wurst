package PlayerData

import LinkedList

import initlater Cell
import initlater Board
import initlater BoardStack
import initlater Hero
import initlater UIButton
import initlater UIEntityWindow
import initlater UIWorldConsole
import initlater UINPCWindow
import initlater UIDialog
import initlater UIHeroWindow
import initlater KeyPressSystem
import initlater BoardEntity
import initlater UISpellBook

public abstract class KeyPressListener
    abstract function onEvent(Player p, string key)

public tuple mousePosition(Cell cell, Cell nearbyCell)

public Player array playerData
constant LinkedList<Player> activePlayers = new LinkedList<Player>

public class Player
    boolean mousePressed = false
    int mousePressedIndex = 0
    UIButton mouseOverButton = null

    DialogInfo dialogInfo = new DialogInfo()

    UIWorldConsoleInfo worldConsoleInfo = new UIWorldConsoleInfo()
    UINPCWindowInfo npcWindowInfo = new UINPCWindowInfo()
    UIEntityWindowInfo stackWindowInfo = new UIEntityWindowInfo()
    UIHeroWindowInfo heroWindoInfo = new UIHeroWindowInfo()
    UISpellBookInfo spellBookInfo = new UISpellBookInfo()

    mousePosition currentMousePosition

    BoardEntity currentSelectedEntity

    Board playingBoard
    Hero hero
    boolean mouse3DEnabled = true
    boolean isConntrolledByKI
    BorderColor borderColor
    color c

    LinkedList<KeyPressListener> keyPressListener = new LinkedList<KeyPressListener>()

    player blzPlayer

    static Player NEUTRAL_AGGRESIVE

    private construct(player p, int i, BorderColor color)
        blzPlayer = p
        isConntrolledByKI = p.isIngame() == false
        
        let playerColor = i <= 2 ? i.toPlayerColor() : PLAYER_COLOR_PEANUT
        this.c = playerColor.toColor()
        this.borderColor = color
        blzPlayer.setColor(playerColor)

        if isConntrolledByKI == false
            registerPlayerOnAnyKeyPresses(p)
            activePlayers.add(this)

    function isLocal() returns boolean
        return blzPlayer == localPlayer

    function toString() returns string
        return blzPlayer.getNameColored()

    function onAnyKeyPress(KeyPressListener listener)
        keyPressListener.add(listener)

    static function setup()
        playerData[players[0].getId()] = new Player(players[0], 0, BorderColor.RED)
        playerData[players[1].getId()] = new Player(players[1], 1, BorderColor.BLUE)
        playerData[players[PLAYER_NEUTRAL_AGGRESSIVE].getId()] = new Player(players[PLAYER_NEUTRAL_AGGRESSIVE], PLAYER_NEUTRAL_AGGRESSIVE, BorderColor.BLACK)
        
        NEUTRAL_AGGRESIVE = playerData[players[PLAYER_NEUTRAL_AGGRESSIVE].getId()]

    static function forEachActive(LLItrClosure<Player> itr)
        activePlayers.forEach(itr)


public function player.getData() returns Player
    return playerData[this.getId()]

public function GetTriggerPlayerData() returns Player
    return GetTriggerPlayer().getData()
        