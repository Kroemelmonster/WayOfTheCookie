package KStringUtils
import NoWurst

import Colors
import KUtil
import Integer
import Real

public enum IconName
    FIRE
    LIGHTNING
    ICE
    PHYSICAL
    MAGIC
    DEATH
    CHAOS

tuple texticon(string unicode, string color)

public class KStringUtils
    static constant COLOR_PRIMARY = colorA(255, 204, 0, 255).toColorString()
    static constant COLOR_NEGATIVE = color(200,50,50).toColorString()
    static constant COLOR_SECONDARY = color(145, 145, 145).toColorString()
    static constant COLOR_POSITIVE = color(75, 214, 19).toColorString()

    static constant string array ICONS = [
        coloredString(colorA(248, 149, 0, 255).toColorString(), "ğŸ”¥"),
        coloredString(colorA(252, 255, 67, 255).toColorString(), "âš¡"),
        coloredString(colorA(124, 237, 252, 255).toColorString(), "â„"),
        coloredString(colorA(179, 179, 179, 255).toColorString(), "âš”"),
        coloredString(colorA(163, 64, 255, 255).toColorString(), "âœ¨"),
        coloredString(colorA(255, 255, 255, 255).toColorString(), "â˜ "),
        coloredString(colorA(21, 255, 0, 255).toColorString(), "ğŸ’€")
    ]
       // "ğŸ”¥","âš¡â„ğŸŒŠğŸŒªâš”ğŸ—¡ğŸ¹ğŸ›¡â˜„âœ¨ğŸ’€â˜ "]
    static constant string array ROMAN_LETTERS = ["I", "II", "III", "IV", "V"]

    static function coloredRange(string color, intRange r) returns string
        var str = color + r.min.toString()
        if r.min < r.max
            str +=  "|r - " + color + r.max.toString()
        str += "|r"
        return str

    static function range(intRange r) returns string
        var str = r.min.toString()
        if r.min < r.max
            str +=  " - " + r.max.toString()
        return str

    static function icon(IconName name) returns string
        return ICONS[name castTo int]

    static function line() returns string
        return "----------"

    static function coloredInt(string color, int value) returns string
        return color + value.toString() + "|r"

    static function coloredString(string color, string value) returns string
        return color + value + "|r"

    static function primaryString(string value) returns string
        return COLOR_PRIMARY + value + "|r"

    static function secondaryString(string value) returns string
        return COLOR_SECONDARY + value + "|r"

    private static function statInner(string prefix, string data) returns string
        return prefix + " : " + data

    static function stat(string prefix, string color, intRange value) returns string
        return statInner(prefix, coloredRange(color, value))

    static function stat(string prefix, intRange value) returns string
        return statInner(prefix, coloredRange(COLOR_PRIMARY, value))

    static function stat(string prefix, string color, string value) returns string
        return statInner(prefix, coloredString(color, value))

    static function stat(string prefix, string color, int value) returns string
        return statInner(prefix, coloredInt(color, value))

    static function stat(string prefix, int value) returns string
        return statInner(prefix, coloredInt(COLOR_PRIMARY, value))

    static function stat(string prefix, string value) returns string
        return statInner(prefix, coloredString(COLOR_PRIMARY, value))

    static function coloredModiInt(int value) returns string
        if value > 0
            return coloredString(COLOR_POSITIVE, " +" + value.toString())
        else if value < 0
            return coloredString(COLOR_NEGATIVE, " " + value.toString())
        return ""

    static function coloredModiReal(real value, int precision) returns string
        if value > 0.0
            return coloredString(COLOR_POSITIVE, " +" + value.toString(precision))
        else if value < 0.0
            return coloredString(COLOR_NEGATIVE, " " + value.toString(precision))
        return ""

    static function percent(real perc) returns string
        return (perc * 100).toString(1) + "%"

    static function coloredPercent(real perc) returns string
        if perc > 0
            return coloredString(COLOR_POSITIVE, "+" + percent(perc))
        else if perc < 0
            return coloredString(COLOR_NEGATIVE, percent(perc))
        return coloredString(COLOR_SECONDARY, percent(perc))

    static function primaryPercent(real perc) returns string
        return coloredString(COLOR_PRIMARY, percent(perc))

public function int.toRomanLetters() returns string
    return KStringUtils.ROMAN_LETTERS[this - 1]