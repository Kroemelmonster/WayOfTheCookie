package CounterAttackPassive

import Cell
import StackPassive

import BoardTrigger
import BoardEntity
import SubStackActions

init
    Initializer.register(InitializerNames.PASSIVES) -> 
        CounterAttackPassive.setup()

public class CounterAttackPassive extends StackPassive
    static StackPassiveType TYPE
    protected static function setup()
        TYPE = new StackPassiveType("Counter Attack", "Icons/PASCounterAttack.dds")
        TYPE.onCreate() (BoardStack stack, StackPassiveData data) -> 
            return new CounterAttackPassive(stack, data)
        TYPE.onCreateTooltipLines() data -> 
            return createTooltipLines(data)
        TYPE.onCreateData() -> 
            return StackPassiveData.create()
            ..setIntData(COUNTER, 1)

    static constant int COUNTER = 0

    private static function createTooltipLines(int counter, int maxCounter) returns LinkedList<string>
        let lines = new LinkedList<string>
        if counter == -1
            lines.add("Every round this unit will counter up to "
             + KStringUtils.primaryString(maxCounter.toString()) 
             + " attacks, with a basic attack.")
        else
            lines.add("Every round this unit will counter up to "
             + KStringUtils.primaryString(counter.toString())
             + " / "
             + KStringUtils.primaryString(maxCounter.toString())
             + " attacks, with a basic attack.")
        return lines

    private static function createTooltipLines(StackPassiveData data) returns LinkedList<string>
        return createTooltipLines(-1, data.getIntData(COUNTER))
      
    private static function createTooltipLines(CounterAttackPassive action) returns LinkedList<string>
        return createTooltipLines(action.counter, action.data.getIntData(COUNTER))

/* -------------------------------------------------------------------------- */

    private int counter

    construct(BoardStack stack, StackPassiveData data)
        super(stack, TYPE, data)
        this.counter = data.getIntData(COUNTER)
        stack.getTriggers().addTurnEnd() (SequenzListener next) ->
            counter = data.getIntData(COUNTER)
            next.continue()

        stack.getTriggers().addOnAttacked() (BoardStack attacker, BoardStack defender, SequenzListener next) ->
            if attacker.isAlive() and defender.isAlive() and checkForCounterAttack(attacker, attacker.getCell())
                SubStackActions.attack(stack, attacker, false) (attackStatus) ->
                    counter --
                    next.continue()
            else
                next.continue()

    private function checkForCounterAttack(BoardStack _attacker, Cell fromCell) returns boolean
        if counter <= 0
            return false

        let distance = fromCell.getDistanceTo(stack.getCell())
        return distance.isBetween(stack.stats.getRange())

    override function createTooltipLines() returns LinkedList<string>
        return createTooltipLines(this)