package UIExtensionArmyPanel

import UIWindow
import Stack
import UIButton
import PlayerData
import initlater Hero
import UIHandle
import UIButtonIcon
import UIDialogSplitStack
import initlater Scheduler
import initlater UIWindowEntity
import UIWindowDropdown

public class UIExtensionArmyPanelInfo
    protected int startDragMousePressedIndex = -1

    protected Army army = null
    protected int dragStart = -1
    protected ArmyStack stackDragStart
    protected int dragEnd = -1
    protected ArmyStack stackDragEnd

    protected static function get(Player p) returns UIExtensionArmyPanelInfo
        return p.worldConsoleInfo.armyInfo
        // TODO make this cool

    protected function setupDragStart(int i)
        stackDragStart = army.getAt(i)
        if stackDragStart == null
            dragEnd = -1
            stackDragEnd = null
            dragStart = -1
        else
            dragStart = i


    protected function setupDragEnd(int i)
        stackDragEnd = army.getAt(i)
        dragEnd = i

class UIExtensionArmyButton extends UIButtonIcon
    protected int index

    construct(UIHandle parent, int index)
        super(parent.self, true, false, false, true)
        setSize(0.0413, 0.0413)
        this.index = index
        enableClick(MOUSE_BUTTON_TYPE_RIGHT, true)

        onClick() (p, mouseBtn) -> 
            // TODO auslagern
            let stack = p.worldConsoleInfo.armyInfo.army.getAt(index)
            if stack != null
                if mouseBtn == MOUSE_BUTTON_TYPE_LEFT
                    actionOpenStackInfo(p, stack)
                else if mouseBtn == MOUSE_BUTTON_TYPE_RIGHT
                    openDropDown(p, stack)

        onMouseDown() (p, mouseBtn) -> 
            if mouseBtn == MOUSE_BUTTON_TYPE_LEFT
                let info = p.worldConsoleInfo.armyInfo
                info.setupDragStart(index)
                info.startDragMousePressedIndex = p.mousePressedIndex

        onMouseUp() (p, mouseBtn) -> 
            let info = p.worldConsoleInfo.armyInfo
            if mouseBtn == MOUSE_BUTTON_TYPE_LEFT
                UIExtensionArmyPanel.dragEnd(p)
                info.startDragMousePressedIndex  = -1
                    
                /*
                let stack = info.army.getAt(index)
                if stack != null
                    UIDialogSplitStack.open(p, stack)
                    */
            
        onMouseEnter() (p) -> 
            let info = p.worldConsoleInfo.armyInfo

            if info.dragStart >= 0
                info.setupDragEnd(index)

    override function disable()
        super.disable()
        setTexture("Icons/BTNDotted.dds")

    protected function actionOpenStackInfo(Player p, ArmyStack stack)
        UIWindowEntity.open(p, stack)

    protected function actionSplitStack(Player p, ArmyStack stack)
        UIDialogSplitStack.open(p, stack)

    protected function actionDeleteStack(Player p, ArmyStack stack)
        p.hero.army.remove(stack)

    protected function openDropDown(Player p, ArmyStack stack)
        if stack == null
            return
        let dropDown = UIWindowDropdown.start(p, this.self)
        dropDown.addOption("Info", true) (p) ->
            actionOpenStackInfo(p, stack)
        dropDown.addOption("Split", stack.getAmount() > 1) (p) ->
            actionSplitStack(p, stack)
        dropDown.addOption("Delete", true) (p) ->
            actionDeleteStack(p, stack)
        dropDown.open()
    
    protected function draw(UIExtensionArmyPanelInfo info)
        let stack = info.army.getAt(index)
        setAlpha(255)
        enable()
        // wenn wir auf startDrag sind zeige potentiell endrag an
        if info.dragStart == index
            // zeige das an was auf dragEnd ist an
            if info.stackDragEnd != null
                setTexture(info.stackDragEnd.typ.getIcon())
                setAmount(info.stackDragEnd.getAmount())
                // sind die nicht identisch wird es ein swap ansonten haben wir gerade einfach nur den normalen status angezeigt
                if info.stackDragEnd != info.stackDragStart
                    setAlpha(80)
            
            else if info.dragEnd > 0
                disable()
        else if info.dragEnd == index
            // zeige das was auf dragStart ist an auÃŸer das sind wir selber
            if info.stackDragStart != info.stackDragEnd
                setTexture(info.stackDragStart.typ.getIcon())
                setAmount(info.stackDragStart.getAmount())
                setAlpha(80)
        else if stack != null
            // there is not drag but maybe there is astack to show
            setTexture(stack.typ.getIcon())
            setAmount(stack.getAmount())
        else
            // there is nothing sooo ?
            disable()


public class UIExtensionArmyPanel
    protected static UIExtensionArmyButton array[ARMY_SIZE] btns
    // protected static UIStackSplitDialog dialogStackSplit

    static function create(UIMenu leftMenu) returns UIMenu
        let element = UIMenu.create(leftMenu)
        ..setPoint(FRAMEPOINT_BOTTOMLEFT, leftMenu, FRAMEPOINT_BOTTOMRIGHT)
        ..setSize(0.258, 0.076)
        
        createUIFrame("BACKDROP", element)
        ..setAllPoints(element)
        ..setTexture("UI/WorldConsole/WorldConsoleArmyPanelBackground.dds")
        
        btns[0] = new UIExtensionArmyButton(element, 0)
        ..setPoint(FRAMEPOINT_BOTTOMLEFT, element, FRAMEPOINT_BOTTOMLEFT, 0.011, 0.007)
        
        for i = 1 to ARMY_SIZE - 1
            btns[i] = new UIExtensionArmyButton(element, i)
            ..setPoint(FRAMEPOINT_LEFT, btns[i - 1], FRAMEPOINT_RIGHT, 0.002, 0)

        Scheduler.onTime() p ->
            if element.isVisible(p)
                update(p)

        element.onHide() p ->
            cancelDrag(p)

        return element

    static function initHero(Hero h)
        let info = h.getPlayer().worldConsoleInfo.armyInfo
        info.army = h.army

    private static function cancelDrag(Player p)
        let info = p.worldConsoleInfo.armyInfo
        info.setupDragStart(-1)

    private static function update(Player p)
        let info = p.worldConsoleInfo.armyInfo
        // are you currently Dragging ?
        if info.dragStart >= 0
            // mouse stopped or mouseDragINdex is different ?
            if not p.mousePressed or p.mousePressedIndex != info.startDragMousePressedIndex
                // stop any drag
                info.setupDragStart(-1)

        if not p.isLocal()
            return

        for i = 0 to ARMY_SIZE - 1
            btns[i].draw(info)
        
    protected static function dragEnd(Player p)
        let info = p.worldConsoleInfo.armyInfo
        if info.stackDragStart == null
            return
        if info.stackDragEnd == info.stackDragStart
            return

        if info.stackDragEnd != null and info.stackDragEnd.typ == info.stackDragStart.typ
            info.stackDragEnd.addAmount(info.stackDragStart.getAmount())
            info.army.setStack(info.dragStart, null)
            destroy info.stackDragStart
        else if info.dragEnd >= 0
            info.army.setStack(info.dragStart, info.stackDragEnd)
            info.army.setStack(info.dragEnd, info.stackDragStart)