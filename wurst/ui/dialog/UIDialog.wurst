package UIDialog

import PlayerData

import UIWindow
import UIButtonMenu
import UIHandle
import UIInteractable

public class UIDialog extends UIWindowBasic
    static function create(string name) returns UIDialog
        return new UIDialog(name)

    private InteractableListener onAccept
    private UIButtonMenu acceptBtn
    private framehandle handleHeader

    private construct(string name)
        super(name, true, true)
        setPointToConsoleUI(FRAMEPOINT_CENTER, FRAMEPOINT_CENTER)
        setSize(0.35, 0.15)
        // TODO dynamicly change level depning on last clicked dilaog / window
        setLevel(1000)

        handleHeader = createUIFrameByName("TextDialogHeader", self, false)
        ..setInnerPoint(FRAMEPOINT_TOP, this, FRAMEPOINT_TOP)
        ..setTextAlignment(TEXT_JUSTIFY_TOP, TEXT_JUSTIFY_CENTER)
        ..setSize(0.3, 0.08)

        acceptBtn = addButton(FRAMEPOINT_BOTTOMLEFT, "Accept") (p) ->
            if onAccept != null
                onAccept.onEvent(p)
            closeWithoutTrigger(p)

        addButton(FRAMEPOINT_BOTTOMRIGHT, "Cancel") (p) -> 
            close(p)

    function onAccept(InteractableListener onAccept)
        this.onAccept = onAccept

    function setHeader(string text)
        handleHeader.setText(text)

    function enableAccept(boolean enable)
        acceptBtn.setEnabled(enable)

    function addButton(framepointtype framepoint, string text, InteractableListener onClick) returns UIButtonMenu
        return UIButtonMenu.create(this, text)
        ..setInnerPoint(framepoint, this, framepoint)
        ..setScale(0.8)
        ..setWidth(0.1)
        ..onClick() (p, mouseBtn) -> 
            onClick.onEvent(p) // TODO this should maybe not triger an event


        

