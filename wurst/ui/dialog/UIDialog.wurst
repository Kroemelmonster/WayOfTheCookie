package UIDialog

import ClosureFrames

import PlayerData

import initlater UIStackSellDialog
import initlater UIStackSplitDialog
import initlater UIFightResultDialog

public class DialogInfo
    UIDialog current = null

    StackSellDialogInfo sellInfo = new StackSellDialogInfo()
    StackSplitDialogInfo splitInfo = new StackSplitDialogInfo()
    FightResultDialogInfo fightResultInfo = new FightResultDialogInfo()

public abstract class UIDialog
    protected framehandle self
    protected framehandle pane

    protected framehandle cancelBtn

    construct(boolean withCancel)
        self = createFrame("BasicBorderWindow", GAME_UI, 0, 0)
        ..setSize(0.25, 0.12)
        ..setVisible(false)
        ..setLevel(10)
        ..setPoint(FRAMEPOINT_CENTER, GAME_UI, FRAMEPOINT_CENTER)
        pane = getFrame("WindowPane")

        if withCancel
            createFrame("DialogCancelButton", pane, 0, 0)
            ..setPoint(FRAMEPOINT_BOTTOMRIGHT, pane, FRAMEPOINT_BOTTOMRIGHT, 0.0, 0.0)
            ..setLevel(100)
            ..onClick() ->
                cancel(GetTriggerPlayerData())
            getFrame("DialogButtonText").setText("Cancel")


    function show(Player p)
        if p.dialogInfo.current != null
            p.dialogInfo.current.cancel(p)
        p.dialogInfo.current = this
        self.setVisible(p.blzPlayer, true)

    protected function hide(Player p)
        self.setVisible(p.blzPlayer, false)
        p.dialogInfo.current = null

    function cancel(Player p)
        hide(p)
        cancelInner(p)

    protected abstract function cancelInner(Player p)
