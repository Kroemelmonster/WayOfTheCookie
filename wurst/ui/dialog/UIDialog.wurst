package UIDialog

import ClosureFrames

import PlayerData

import UIWindow
import UIButtonMenu
import initlater UIDialogSplitStack
import UIHandle

public class DialogInfo
    DialogInfoSplitStack splitInfo = new DialogInfoSplitStack()

public class UIDialog extends UIWindowBasic
    static function create(string name) returns UIDialog
        return new UIDialog(name)

    private FrameHandleListener onAccept
    private framehandle handleHeader

    private construct(string name)
        super(name, true)
        setPointToConsoleUI(FRAMEPOINT_CENTER, FRAMEPOINT_CENTER)
        setSize(0.35, 0.15)
        // TODO dynamicly change level depning on last clicked dilaog / window
        setLevel(1000)

        handleHeader = createUIFrameByName("TextDialogHeader", self, false)
        ..setInnerPoint(FRAMEPOINT_TOP, this, FRAMEPOINT_TOP)
        ..setTextAlignment(TEXT_JUSTIFY_TOP, TEXT_JUSTIFY_CENTER)
        ..setSize(0.1, 0.2)

        addButton(FRAMEPOINT_BOTTOMLEFT, "Accept") ->
            if onAccept != null
                onAccept.onEvent()
            closeWithoutTrigger(GetTriggerPlayerData())

        addButton(FRAMEPOINT_BOTTOMRIGHT, "Cancel")  -> 
            close(GetTriggerPlayerData())

    function onAccept(FrameHandleListener onAccept)
        this.onAccept = onAccept

    function setHeader(string text)
        handleHeader.setText(text)

    function addButton(framepointtype framepoint, string text, FrameHandleListener onClick) returns UIButtonMenu
        return UIButtonMenu.create(this, text)
        ..setInnerPoint(framepoint, this, framepoint)
        ..setScale(0.8)
        ..setWidth(0.1)
        ..onClick(onClick)


        

