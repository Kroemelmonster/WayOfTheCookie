package UIDialogSplitStack

import PlayerData
import UISlider

import initlater MainController
import initlater Stack
import UIDialog
import UIHandle
import UIWindow
import UIButtonIcon

init
    Initializer.register(InitializerNames.UI) -> 
        Player.forEachActive() (Player p) ->
            DialogInfoSplitStackInfo.create(p)
        UIDialogSplitStack.create()

class DialogInfoSplitStackInfo
    static DialogInfoSplitStackInfo array data
    static function get(Player p) returns DialogInfoSplitStackInfo
        return data[p castTo int]

    static function get() returns DialogInfoSplitStackInfo
        return get(GetTriggerPlayerData())

    protected static function create(Player p)
        data[p castTo int] = new DialogInfoSplitStackInfo(p)

    protected Player p
    protected Stack stack
    protected int amount
    protected int maxAmount

    private construct(Player p)
        this.p = p

public class UIDialogSplitStack
    private static UIDialog dia
    private static UISlider slider

    private static UIButtonIcon previewBtnLeft
    private static UIButtonIcon previewBtnRight

    protected static function create()
        dia = UIDialog.create("Split")
        ..onAccept() (p) -> 
            split(DialogInfoSplitStackInfo.get(p))

        previewBtnLeft = UIButtonIcon.create(dia, false, false, true, true)
        ..setSize(0.048, 0.048)
        ..setInnerPoint(FRAMEPOINT_TOPLEFT, dia, FRAMEPOINT_TOPLEFT, 0.005, -0.018)
        ..setEnabled(false)

        previewBtnRight = UIButtonIcon.create(dia, false, false, false, true)
        ..setSize(0.048, 0.048)
        ..setInnerPoint(FRAMEPOINT_TOPRIGHT, dia, FRAMEPOINT_TOPRIGHT, -0.005, -0.018)
        ..setEnabled(false)

        slider = UISlider.create(dia)
        ..setPoint(FRAMEPOINT_LEFT, previewBtnLeft, FRAMEPOINT_RIGHT, 0.005, 0.0)
        ..setWidth(dia.getInnerWidth() - (previewBtnLeft.self.getWidth() * 2 + 0.02))
        ..onValueChange() (p) -> 
            setStackValues(DialogInfoSplitStackInfo.get(p), slider.getValue())

    static function open(Player p, Stack stack)
        openInner(DialogInfoSplitStackInfo.get(p), stack)

    static function split(DialogInfoSplitStackInfo info)
        MainController.splitStack(info.p.hero, info.stack, info.amount)
    
    private static function openInner(DialogInfoSplitStackInfo info, Stack stack)
        let startAmount = stack.getAmount()
        if startAmount < 2
            return // TODO this is an error give info to player maybe

        info.stack = stack
        info.amount = (startAmount / 2).round()
        info.maxAmount = startAmount - 1
    
        if info.p.isLocal()
            slider.setMinMax(1, info.maxAmount)
            previewBtnLeft.setTexture(info.stack.typ.getIcon())
            previewBtnRight.setTexture(info.stack.typ.getIcon())
            
            dia.setHeader("Split  "+stack.typ.getName())
            
        dia.open(info.p)
        update(info)

    private static function setStackValues(DialogInfoSplitStackInfo info, int amount)
        info.amount = amount
        if amount > info.maxAmount
            info.amount = info.maxAmount
        if amount < 1
            info.amount = 1

        update(info)

    private static function update(DialogInfoSplitStackInfo info)
        if info.p.isLocal()
            slider.setValue(info.amount)
            previewBtnLeft.setAmount(info.maxAmount - info.amount + 1, info.maxAmount)
            previewBtnRight.setAmount(info.amount, info.maxAmount)