package UIMenuConsole
import UIWindow
import UIHandle
import UIWindowMenu
import PlayerData
import UIButtonMenu
import Hero
import HeroData
import UIWindowSkillTree
import UIWindowEntity
import UIWindowHero
import UIWindowSpellBook

public class UIMenuConsoleInfo
    UIWindowSkillTreeInfo skillTreeInfo = new UIWindowSkillTreeInfo()
    UIWindowEntityInfo entityInfo = new UIWindowEntityInfo()
    UIWindowHeroInfo heroInfo = new UIWindowHeroInfo()

class UIRessource extends UIHandle
    protected static function create(UIHandle parent, real height, real width, string icon) returns UIRessource
        return new UIRessource(parent, height, width, icon)

    private framehandle handleText
    
    private construct(UIHandle parent, real height, real width, string icon)
        super(createUIFrame("BACKDROP", parent))
        setTexture("UI/Console/BackgroundRessource.dds")
        setSize(width, height)

        let padding = 0.003

        let handleIcon = createUIFrame("BACKDROP", self)
        ..setPoint(FRAMEPOINT_LEFT, self, FRAMEPOINT_LEFT, padding, 0)
        ..setSize(height - (padding * 2), height - (padding * 2))
        ..setTexture(icon)

        handleText = createUIFrame("TEXT", self)
        ..setPoint(FRAMEPOINT_LEFT, handleIcon, FRAMEPOINT_RIGHT, padding, 0)
        ..setScale(1.3)
    
    protected function setValue(int value)
        handleText.setText(value.toString())

init
    Initializer.register(InitializerNames.UI) -> 
        UIMenuConsole.create()

public class UIMenuConsole
    protected static UIMenu menu

    protected static UIRessource ressourceLeadership
    protected static UIRessource ressourceGold
    protected static UIRessource ressourceMight
    protected static UIRessource ressourceMind
    protected static UIRessource ressourceMagic

    protected static function create()
        menu = UIMenu.create()
        ..setPointToGameUI(FRAMEPOINT_TOPLEFT, FRAMEPOINT_TOPLEFT)
        ..setPointToGameUI(FRAMEPOINT_TOPRIGHT, FRAMEPOINT_TOPRIGHT)

        let paddingTop = 0.003

        let paddingRessource = 0.003
        let ressourceWidth = 0.09
        let ressourceHeight = 0.024
        ressourceLeadership = UIRessource.create(menu, ressourceHeight, ressourceWidth, "UI/Console/IconLeadership.dds")
        ..setPoint(FRAMEPOINT_TOPRIGHT, menu, FRAMEPOINT_TOPRIGHT, 0, -paddingTop)
        ressourceGold = UIRessource.create(menu, ressourceHeight, ressourceWidth, "UI/Console/IconGold.dds")
        ..setPoint(FRAMEPOINT_RIGHT, ressourceLeadership, FRAMEPOINT_LEFT, -paddingRessource, 0)

        let littleRessourceWidth = (((ressourceWidth * 2) - paddingRessource) / 3)
        ressourceMight = UIRessource.create(menu, ressourceHeight, littleRessourceWidth, "UI/Console/IconMight.dds")
        ..setPoint(FRAMEPOINT_TOPLEFT, ressourceGold, FRAMEPOINT_BOTTOMLEFT, 0, -paddingRessource)
        ressourceMind = UIRessource.create(menu, ressourceHeight, littleRessourceWidth, "UI/Console/IconMind.dds")
        ..setPoint(FRAMEPOINT_LEFT, ressourceMight, FRAMEPOINT_RIGHT, paddingRessource, 0)
        ressourceMagic = UIRessource.create(menu, ressourceHeight, littleRessourceWidth, "UI/Console/IconMagic.dds")
        ..setPoint(FRAMEPOINT_LEFT, ressourceMind, FRAMEPOINT_RIGHT, paddingRessource, 0)

        menu.setHeight(paddingTop + paddingRessource + (ressourceHeight * 2))

        CONSOLE_UI = createUIFrame("FRAME", GAME_UI)
        ..setPoint(FRAMEPOINT_TOPLEFT, menu, FRAMEPOINT_BOTTOMLEFT)
        ..setPoint(FRAMEPOINT_BOTTOMRIGHT, GAME_UI, FRAMEPOINT_BOTTOMRIGHT, 0, -0.1)

        UIWindowEntity.create()

        let paddingBtns = 0.003
        let btnWidth = 0.09
        let btnHeight = 0.02

        var btn = UIButtonMenuToWindow.create(menu, UIWindowMenu.create())
        ..setPoint(FRAMEPOINT_TOPLEFT, menu, FRAMEPOINT_TOPLEFT, 0, -paddingTop)
        ..setSize(btnWidth, btnHeight)

        btn = UIButtonMenuToWindow.create(menu, UIWindowSkillTree.create())
        ..setPoint(FRAMEPOINT_LEFT, btn, FRAMEPOINT_RIGHT, paddingBtns, 0)
        ..setSize(btnWidth, btnHeight)


        btn = UIButtonMenuToWindow.create(menu, UIWindowHero.create())
        ..setPoint(FRAMEPOINT_LEFT, btn, FRAMEPOINT_RIGHT, paddingBtns, 0)
        ..setSize(btnWidth, btnHeight)

        UIButtonMenuToWindow.create(menu, UIWindowSpellBook.create())
        ..setPoint(FRAMEPOINT_LEFT, btn, FRAMEPOINT_RIGHT, paddingBtns, 0)
        ..setSize(btnWidth, btnHeight)


    static function initHero(Hero hero)
        menu.show(hero.getPlayer())
        if hero.getPlayer().isLocal()
            ressourceLeadership.setValue(hero.getLeadership())
            ressourceGold.setValue(hero.getGold())
       
        updateHeroDataInner(hero.getData())

        UIWindowSkillTree.initHero(hero)

    static function updateHeroData(Hero hero, HeroDataChange _changedData)
        if not hero.getPlayer().isLocal()
            return
        updateHeroDataInner(hero.getData())

    static function updateHeroDataInner(HeroData data)
        ressourceMight.setValue(data.getMight())
        ressourceMind.setValue(data.getMind())
        ressourceMagic.setValue(data.getMagic())