package UIWindowMenu
import UIWindow
import PlayerData
import UIHandle
import UIButtonMenu
import FramehandleNames
import UIDialog
import Globals


public class UIWindowMenu
    protected static UIWindowBasic window

    static function create() returns UIWindowBasic
        window = UIWindowBasic.create("Menu", false)
        ..setPointToConsoleUI(FRAMEPOINT_TOP, FRAMEPOINT_TOP)
        ..setSize(0.29, 0.35)
        ..setLevel(1000)

        let btnEnd = UIButtonMenu.create(window, "End Game")
        ..setInnerPoint(FRAMEPOINT_BOTTOMLEFT, window, FRAMEPOINT_BOTTOMLEFT)
        ..setInnerPoint(FRAMEPOINT_BOTTOMRIGHT, window, FRAMEPOINT_BOTTOMRIGHT)
        ..onClick() ->
            EndGame(false)
        
        let btnFrames = UIButtonMenu.create(window, "Frames off")
        ..setPoint(FRAMEPOINT_BOTTOMLEFT, btnEnd, FRAMEPOINT_TOPLEFT, 0, 0.01)
        ..setPoint(FRAMEPOINT_BOTTOMRIGHT, btnEnd, FRAMEPOINT_TOPRIGHT, 0, 0.01)
        btnFrames.onClick() ->
            let ressourceBar = getFrame("ConsoleUI")
            let isVisible = ressourceBar.isVisible()
            ressourceBar.setVisible(not isVisible)
            getFrame("ResourceBarFrame").setVisible(not isVisible)
            if isVisible
                btnFrames.setText("Frames off")
            else
                btnFrames.setText("Frames on")

        let btnKi = UIButtonMenu.create(window, "KI Controlled")
        ..setPoint(FRAMEPOINT_BOTTOMLEFT, btnFrames, FRAMEPOINT_TOPLEFT, 0, 0.01)
        ..setPoint(FRAMEPOINT_BOTTOMRIGHT, btnFrames, FRAMEPOINT_TOPRIGHT, 0, 0.01)
        btnKi.onClick() ->
            canControllKI = not canControllKI
            if canControllKI
                btnKi.setText("User Controlled")
            else
                btnKi.setText("KI Controlled")

        return window

    static function get() returns UIWindowBasic
        return window