package MouseContext

import BoardStack
import Cell
import PlayerData
import UIController
import OrderSystem

import initlater UIMenuBoardConsole
import initlater Mouse3DPositionSystem
import initlater UITooltip
import BoardEntity
import PlayerBoardHelper

class MouseContext
    /** Use this to force a player to select a cell **/
    static function forcePlayerSelect(Player p, Cell cell)
        onMouseSelect(p, mousePosition(cell, null), false)

    /** Use this to simulate a Mouse Entering **/
    static function forceMouseEnter(Player p, Cell cell)
        onMouseEnter(p, mousePosition(cell, null))

    static function forceMouseLeave(Player p)
        onMouseLeave(p)

    /** is used when any userinput should be interpreted as a Highlight of a cell / stack **/
    static function onMouseEnter(Player p, mousePosition mP)
        // dont have to do anything if it is the same cell ( even if both are null )
        /*
            We currently do not care if you change the nearbyCell for vector targeting
            this is due to lag problems. IF you want to enable it again make sure that 
            only used spells actually get an update and not every single action..
            THIS WAS THE OLD CODE : 
            and mP.nearbyCell == p.currentMousePosition.nearbyCell
            */
        if mP.cell == p.currentMousePosition.cell
            return
        
        resetHighlight(p)
        if mP.cell != null
            let stack = mP.cell.getStack()
            if stack != null
                UIMenuBoardConsole.highlightEntity(p, stack)

        p.currentMousePosition = mP
        UIController.triggerOnPlayerCellHighlight(p)

    /** is used as an interpreted version of a userinput leaving ANY cell / stack **/
    private static function onMouseLeave(Player p)
        resetHighlight(p)

    /** is used as an interpreted version of a userinput selecting ANY cell / stack **/
    static function onMouseSelect(Player p, mousePosition mP, boolean manual)
        var entity = getMousePositionEntity(mP)
        var directClick = manual
        if entity == null
            directClick = false
            entity = PlayerBoardHelper.getCurrentActiveEntity(p)

        p.currentSelectedEntity = entity
        UIController.selectEntity(p, entity, directClick)
        UIController.triggerOnPlayerCellHighlight(p)

    /** is used as an interpreted version of a userinput ordering ANY cell / stack **/
    static function onOrder(Player p, mousePosition mP)
        OrderSystem.sendSimpleTargetOrder(p, mP.cell)

    private static function getMousePositionEntity(mousePosition mP) returns BoardEntity
        if mP.cell != null
            return mP.cell.getStack()
        return null

    @inline
    static function enableMouseMovement(Player p, boolean enable)
        Mouse3DPositionSystem.setEnabled(p, enable)
    
    private static function resetHighlight(Player p)
        UIMenuBoardConsole.resetHighlight(p)
        UITooltip.hide(p)

