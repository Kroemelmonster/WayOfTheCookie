package RallingCrySpell

import PlayerData
import UITooltip
import BoardController


import UIActionWindow
import CastAnimation
import BoardTrigger
import Spell
import BoardHero
import BoardEntity
import SkillTree
import StatusType
import StatusController
import Status
import WarcrySpell
import SkillTreeChieftain
import RallingCryBuffHero
import LinkedList
import BoardStack
import RallingCryBuffStack

public class RallingCrySpell extends WarcrySpell
    static SpellType TYPE
    static function setup()
        STATUS = RallingCryBuffHero.TYPE
        STATUS_STACKS = RallingCryBuffStack.TYPE
        TYPE = new SpellType("Ralling Cry", "Icons\\BTNAbilityWarriorWarCry.blp")
        TYPE.onCreate() boardHero -> 
            return new RallingCrySpell(boardHero)

    private static constant int COOLDOWN = 3
    private static constant int array[5] ATTACK_BONUS = [3, 5, 7, 9, 11]
    static StatusType STATUS
    static StatusType STATUS_STACKS

    @inline
    private static function createStatusData(int level) returns StatusData
        return StatusData.createDurationI(1, ATTACK_BONUS[level])

    static function createTooltip(int level) returns Tooltip
        let tt = Tooltip.create(TYPE.getName())
        ..setCooldown(COOLDOWN)
        ..addLine(Tooltip.stat("Target", "Allies/Self"))
        ..addLine("")
        ..addLine("Gives your self and all your troops a temporary Attack Bonus")
        ..addStatusType(STATUS, createStatusData(level))
        return tt
    
    /* -------------------------------------------------------------------------- */

    private LinkedList<BoardStack> affectedAllies = new LinkedList<BoardStack>()

    private construct(BoardHero hero)
        super(hero, TYPE)
        let skillTree = hero.hero.getSkillTree(SkillTreeChieftain.TYPE) castTo SkillTreeChieftain
        setLevel(skillTree.rallingCryLevel)
        this.manacost = 0
        this.maxCooldown = COOLDOWN

    override function activate()
        CastAnimation.create(entity) -> 
            StatusController.add(STATUS, entity, createStatusData(level))
            ..addOnEndListener() (SequenzListener next) ->
                affectedAllies.forEach() (BoardStack ally) ->
                    if ally.isAlive()
                        StatusController.remove(STATUS_STACKS, ally)
                next.continue()

            entity.getBoard().forEachStack() (BoardStack ally) ->
                if ally.getPlayer() == entity.getPlayer()
                    affectedAllies.add(ally)
                    StatusController.add(STATUS_STACKS, ally, createStatusData(level))
                    
            payCostAndStartCooldown(maxCooldown)
            BoardController.finishOrder(entity, true)

    private function showPreview()
        let tt = createTooltip(level)
        tt.display(entity.getPlayer(), true)
        UIActionWindow.updateActionPoints(entity, 0)

    private function hidePreview()
        UITooltip.hide(entity.getPlayer())
        UIActionWindow.updateActionPoints(entity, 0)

    override function onUIMouseEnter()
        showPreview()

    override function onUIMouseLeave()
        hidePreview()

    ondestroy

    /* ----------------------------------- KI ----------------------------------- */
    
    override function executeForKI(SequenzListenerStatus listener)
        Log.trace("YOU NEED TO WRITE KI FOR RALLING CRY SPELL")
        listener.continue(-1)