package HeroSkipAction


import Icons

import Action
import InstantAction
import Abilities

import OrderSystem
import BoardController
import UITooltip
import UIActionWindow

import initlater BoardEntity
import BoardTrigger
import ActionCost

public class HeroSkipAction extends InstantAction
    private constant static string KEY = "S"

    private Tooltip tt

    construct(BoardEntity entity)
        super(entity, Icons.bTNCancel, "Skip", ActionCostPreset.create(0, true))
        tt = Tooltip.create(name, KEY)
        tt.addLine("Skips your turn")
        
    override function canActivate() returns boolean
        return true

    override function activate()
        BoardController.finishOrder(entity, cost)
        
    override function onUIMouseEnter()
        tt.display(entity.getPlayer(), false)
        UIActionWindow.updateActionPoints(entity, cost)

    override function onUIMouseLeave()
        UITooltip.hide(entity.getPlayer())
        UIActionWindow.updateActionPoints(entity)

    override function canBeActivatedByKey(string key) returns boolean
        return key == KEY

    override function executeForKI(SequenzListenerStatus listener)
        activate()
        listener.continue(0)

    ondestroy
        destroy tt