package Spell
import Action
import initlater BoardHero
import ActionCost
import ActionType
import BoardEntity

public abstract class Spell extends Action
    protected BoardHero hero
    protected int level
    protected boolean quickCast = false

    static function createType(string name, string icon) returns ActionType
        return new ActionType(name, icon)
        ..enableOption(ActionTypeOption.SPELL)

    construct(BoardEntity entity, ActionType actionType, ActionCostPreset cost)
        super(entity, actionType, cost)
        if entity.getType() != EntityType.HERO
            Log.error("Tried to add a Spell to an non Hero")
        this.hero = entity.toHero()
        activateQuickcast(true)

    function setLevel(int i)
        if i < 0
            Log.error("Tried to create the Spell " + getName() 
             + " with a level of " + i.toString())
        this.level = i

    function isQuickcast() returns boolean
        return quickCast

    function toggleQuickcast()
        activateQuickcast(not quickCast)

    function activateQuickcast(boolean flag)
        if flag
            hero.updateQuickcast()
            quickCast = true
        else
            quickCast = false

    override function showAsPrimary() returns boolean
        return quickCast