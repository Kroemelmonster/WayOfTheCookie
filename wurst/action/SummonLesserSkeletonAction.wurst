package SummonLesserSkeletonAction

import LinkedList
import Icons

import PlayerData
import KUtil
import UITooltip
import BoardController
import OrderSystem

import Action
import SummonLesserSkeletonAnimation
import StackType

import initlater Cell
import initlater BoardStack
import initlater StackTypesDefinition
import UIActionWindow

public class SummonLesserSkeletonAction extends Action
    private LinkedList<Cell> area = new LinkedList<Cell>()
    private Tooltip tooltip
    private intRange summonAmount  

    private static constant int COST = 2
    private static constant int COOLDOWN = 3
    private static constant int MAXRANGE = 1
    private static constant string NAME = "Summon Skeleton"
    private static StackType SUMMONED_TYPE
    private constant static oskeytype KEY = OSKEY_S
    private static Tooltip BASE_TOOLTIP
    static constant string ICON = Icons.bTNRaiseDead

    static function setup()
        SUMMONED_TYPE = StackTypes.LESSER_SKELETON
        BASE_TOOLTIP = Tooltip.create(NAME)
        ..setCooldown(COOLDOWN)
        ..setCost(COST)
        
        ..addLine(Tooltip.stat("Target", "Empty Cell"))
        ..addLine(Tooltip.stat("Range", MAXRANGE))
        ..addLine("")
        ..addLine("")

    private static function getSummonLeadershipAmount(StackType sType) returns intRange
        return intRange((sType.leadership * 0.3).ceil(), (sType.leadership * 0.6).ceil())

    static function createTooltip(StackType sType) returns Tooltip
        let leadership = getSummonLeadershipAmount(sType)
        let str = "Summons a Stack of "+ Tooltip.coloredString(Tooltip.COLOR_SECONDARY, SUMMONED_TYPE.name) +" with a Leaderhip of "+Tooltip.coloredRange(Tooltip.COLOR_PRIMARY, leadership)
        let tt = Tooltip.copy(BASE_TOOLTIP)
        ..setLine(3, str)

        return tt

    construct(BoardStack stack)
        super(stack, ICON, NAME)
        tooltip = Tooltip.copy(BASE_TOOLTIP)
        ..addKey("S")

    override function getHighlightResultForCell(Player p, Cell cell) returns CellResult
        if area.has(cell)
            return CellResult.NEUTRAL
        return CellResult.NONE

    override function recalculatePreview()
        recalculate()

    override function recalculateHighlight(Player p)
        if area.has(p.currentMousePosition.cell)
            showPreview()
        else
            hidePreview()

    override function sendTargetOrder(Cell cell) returns boolean
        if area.has(cell)
            recalcSummonAmount()
            let summon = cell.getBoard().createSummon(stack.getPlayer(), SUMMONED_TYPE, summonAmount.random())

            let anim = new SummonLesserSkeletonAnimation(stack, cell) () ->
                BoardController.placeStackOnBoard(summon, cell)

            anim.onFinish() () ->
                cooldown = COOLDOWN + 1
                BoardController.finishAction(stack)
                OrderSystem.endOrder(stack.getBoard())
            
            return true
        
        return false

    private function recalcSummonAmount()
        let amount = stack.stats.getAmount()
        let leadershipAmount = getSummonLeadershipAmount(stack.getStackType())
        summonAmount = intRange((leadershipAmount.min * amount / SUMMONED_TYPE.leadership).ceil(), (leadershipAmount.max * amount / SUMMONED_TYPE.leadership).ceil())
        
        var str = "Summons a Stack of "+ Tooltip.coloredRange(Tooltip.COLOR_PRIMARY, summonAmount)
        str += " " + Tooltip.coloredString(Tooltip.COLOR_SECONDARY, SUMMONED_TYPE.name)
        tooltip.setLine(3, str)

    private function recalculate()
        area.clear()
        recalcSummonAmount()

        stack.getCell().forEachCellsInRange(intRange(1, MAXRANGE)) (Cell cell) ->
            if cell.getStack() == null
                area.add(cell)

    override function canActivate() returns boolean
        return stack.stats.getRemainingActionPoints() >= COST and cooldown <= 0

    @inline
    private function showPreview()
        tooltip.display(stack.getPlayer(), false)
        UIActionWindow.updateActionPoints(stack, 99)
                
    @inline
    private function hidePreview()
        UITooltip.hide(stack.getPlayer())
        UIActionWindow.updateActionPoints(stack, 0)

    override function onUIMouseEnter()
        recalcSummonAmount()
        showPreview()

    override function onUIMouseLeave()
        hidePreview()

    override function canBeActivatedByKey(oskeytype key) returns boolean
        return key == KEY

    ondestroy
        if area != null
            destroy area
        if tooltip != null
            destroy tooltip
        