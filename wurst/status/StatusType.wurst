package StatusType

import Status
import UITooltip
import BleedingAilment
import PoisonAilment
import StunDebuff
import SlowDebuff
import HastedBuff
import PurgedDebuff
import BoardEntity

interface StatusCreateListener
    function create(BoardEntity entity) returns Status

interface StatusTypeTooltipListener
    function getTooltip(StatusData data) returns Tooltip

public class StatusTypeSet
    static StatusType BLEEDING
    static StatusType POISON
    static StatusType STUNNED
    static StatusType SLOWED
    static StatusType PURGED
    static StatusType HASTED

    static function setup()
        BLEEDING    = BleedingAilment.setup()
        POISON      = PoisonAilment.setup()
        STUNNED     = StunDebuff.setup()
        SLOWED      = SlowDebuff.setup()
        HASTED      = HastedBuff.setup()
        PURGED      = PurgedDebuff.createType()


public class StatusType
    protected string icon
    protected string name
    protected StatusCreateListener listener
    protected StatusTypeTooltipListener tooltipCreator

    construct(string icon, string name)
        this.icon = icon
        this.name = name

    function setCreationListener(StatusCreateListener listener)
        this.listener = listener

    function setTooltipListener(StatusTypeTooltipListener listener)
        this.tooltipCreator = listener

    @inline
    function createNew(BoardEntity entity) returns Status
        return listener.create(entity)

    @inline
    function getIcon() returns string
        return icon
    
    @inline
    function getName() returns string
        return name

    @inline
    function getTooltip(StatusData data) returns Tooltip
        return tooltipCreator.getTooltip(data)

    @inline
    function toString() returns string
        return "StatusType '"+name+"'"
