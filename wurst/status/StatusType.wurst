package StatusType

import Status
import AilmentBleeding
import AilmentPoison
import StatusStunned
import StatusPurged
import BoardEntity
import StatusRallingCry
import LinkedList
import Tooltip

interface StatusCreateListener
    function create(BoardEntity entity) returns Status

interface StatusCreateTooltipLinesListener
    function create(StatusData data) returns LinkedList<string>

public class StatusType
    protected string name
    protected string icon
    protected StatusCreateListener listener
    protected StatusCreateTooltipLinesListener tooltipLinesListener

    construct(string name, string icon)
        this.icon = icon
        this.name = name

    static function setup()
        AilmentBleeding.setup()
        AilmentPoison.setup()
        StatusStunned.setup()
        StatusPurged.setup()
        StatusRallingCryStack.setup()
        StatusRallingCryHero.setup()
        
    function setCreationListener(StatusCreateListener listener)
        this.listener = listener

    function setTooltipListener(StatusCreateTooltipLinesListener listener)
        this.tooltipLinesListener = listener

    function createNew(BoardEntity entity) returns Status
        return listener.create(entity)

    function getIcon() returns string
        return icon
    
    function getName() returns string
        return name

    function createTooltipLines() returns LinkedList<string>
        return tooltipLinesListener.create(null)
        ..addtoStart(name)

    function createTooltipLines(StatusData data) returns LinkedList<string>
        return tooltipLinesListener.create(data)
        ..addtoStart(name)

    function createTooltip() returns Tooltip
        return createTooltip(null)

    function createTooltip(StatusData data) returns Tooltip
        return Tooltip.create(name)
        ..addLines(createTooltipLines(data))

    function toString() returns string
        return "StatusType '"+name+"'"
