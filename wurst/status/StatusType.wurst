package StatusType

import Status
import AilmentBleeding
import AilmentPoison
import StatusStunned
import StatusPurged
import BoardEntity
import StatusRallingCry
import LinkedList
import Tooltip
import KStringUtils
import KUtil

interface StatusCreateListener
    function create(BoardEntity entity, StatusData data) returns Status

interface StatusCreateTooltipLinesListener
    function create(StatusData data) returns LinkedList<string>

public class StatusType
    protected string name
    protected string icon
    protected StatusCreateListener listener
    protected StatusCreateTooltipLinesListener tooltipLinesListener

    construct(string name, string icon)
        this.icon = icon
        this.name = name

    static function setup()
        StatusStunned.setup()
        StatusPurged.setup()
        StatusRallingCry.setup()
        
    function setCreationListener(StatusCreateListener listener)
        this.listener = listener

    function setTooltipListener(StatusCreateTooltipLinesListener listener)
        this.tooltipLinesListener = listener

    /** Pls use the StatusController to apply and remove stati */
    function applyNewStatusTo(BoardEntity entity, StatusData data) returns Status
        let status = listener.create(entity, data)
        entity.addStatus(status)
        if data != null
            destroy data
        return status

    function getIcon() returns string
        return icon
    
    function getName() returns string
        return name

    function createTooltipLines() returns LinkedList<string>
        return createTooltipLines(null)

    function createTooltipLines(StatusData data) returns LinkedList<string>
        let lines = new LinkedList<string>()
        ..add(KStringUtils.stat("Status", KStringUtils.secondaryString(name)))
        ..addAllAndDestroy(tooltipLinesListener.create(data))
        if data != null
            destroy data
        return lines

    function createTooltip() returns Tooltip
        return createTooltip(null)

    function createTooltip(StatusData data) returns Tooltip
        return Tooltip.create(name)
        ..addLines(createTooltipLines(data))

    function toString() returns string
        return "StatusType '"+name+"'"
