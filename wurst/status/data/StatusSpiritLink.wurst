package StatusSpiritLink

import AttachmentPoints
import Icons
import Abilities

import Status

import initlater BoardStack
import initlater BoardEntity
import initlater StatusType
import BoardController
import BoardTrigger
import Tooltip
import Damage
import AnimationWait
import SharedStatus
import StatusController

init
    Initializer.register(InitializerNames.STATI) -> 
        StatusSpiritLink.setup()

public class StatusSpiritLink extends SharedStatus
    static StatusType TYPE
    protected static function setup()
        TYPE = new StatusType("Spirit Link", Icons.bTNSpiritLink)
        TYPE.setCreationListener() (boardEntity, data) ->
            return new StatusSpiritLink(TYPE, boardEntity, data)
        TYPE.setTooltipListener() data ->
            return createTooltipBaseLines(data)
        TYPE.setCanApplyListener() (BoardEntity entity, StatusData data) ->
            return canApplyTo(entity, data)

    static constant int DAMAGE_TRANSFER = 0
    static constant int DAMAGE_REDUCE = 1

    private static function canApplyTo(BoardEntity entity, StatusData data) returns boolean
        let status = StatusController.get(TYPE, entity) castTo StatusSpiritLink
        if status != null and not status.isPrimary()
            return false
        let target = data.getEntityData()
        return target != null and not StatusController.has(TYPE, target)

    private static function createTooltipBaseLinesPrimary(real transferPerc, real reducePerc) returns LinkedList<string>
        return new LinkedList<string>
        ..add(KStringUtils.primaryPercent(transferPerc) + " of any Damage your linked Allies take gets transfered to you.")
        ..add("You only take " + KStringUtils.primaryPercent(reducePerc) + " of the transfered Damage.")

    private static function createTooltipBaseLines(StatusData data) returns LinkedList<string>
        return createTooltipBaseLinesPrimary(data.getRealData(DAMAGE_TRANSFER), data.getRealData(DAMAGE_REDUCE))

    private static function createTooltip(StatusSpiritLink b) returns Tooltip
        let tt = Tooltip.create(TYPE.getName())
        if b.isPrimary()
            tt.addLine(KStringUtils.stat("Duration", b.duration))
            var targetStr = ""
            let size = b.secondaryStati.size()
            if size == 1
                targetStr = b.secondaryStati.getFirst().entity.getName()
            else if size > 1
                targetStr = size.toString() + " Allies"
            tt.addLine(KStringUtils.stat("LinkedTo", targetStr))
            tt.addLine("")
            tt.addLines(createTooltipBaseLinesPrimary(b.transferPerc, b.reducePerc))
        else
            tt.addLine(KStringUtils.stat("LinkedTo", b.primary.entity.getName()))
            tt.addLine("")
            tt.addLine(KStringUtils.primaryPercent(b.transferPerc) + " of any Damage you take gets transfered to the caster.")
        return tt

    /* -------------------------------------------------------------------------- */

    private BoardTrigger turnStartListener
    private BoardTriggerOnDamage damageTakenTrigger
    
    private effect display
    private int duration
    private real reducePerc
    private real transferPerc

    construct(StatusType statusType, BoardEntity entity, StatusData data)
        super(statusType, entity, data)

    protected override function setupPrimary(StatusData data)
        reducePerc = data.getRealData(DAMAGE_REDUCE)
        transferPerc = data.getRealData(DAMAGE_TRANSFER)
        display = entity.addAttachEffect(Abilities.spiritLinkTarget, AttachmentPoints.chest)

        applySecondaryInner(data)

        duration = data.duration
        turnStartListener = entity.getTriggers().addTurnStart() next ->
            duration--
            if duration <= 0
                remove(next)
            else
                next.continue()

    protected function applySecondaryInner(StatusData data)
        let secondaryData = StatusData.create()
        ..setRealData(DAMAGE_REDUCE, reducePerc)
        ..setRealData(DAMAGE_TRANSFER, transferPerc)
        applySecondary(data.getEntityData(), secondaryData)
        destroy secondaryData
        
                
    protected override function setupSecondary(StatusData data)
        reducePerc = data.getRealData(DAMAGE_REDUCE)
        transferPerc = data.getRealData(DAMAGE_TRANSFER)
        display = entity.addAttachEffect(Abilities.spiritLinkTarget, AttachmentPoints.chest)


        damageTakenTrigger = entity.getTriggers().addOnBeforeDamageTaken() (BoardEntity source, BoardStack target, SpecificDamageInstance instance, SequenzListener next) ->
            if instance.hasFlag(DamageFlags.UNMITIGATABLE)
                next.continue()
            else
                absorbDamage(instance, next)
    
    private function absorbDamage(SpecificDamageInstance incommingInstance, SequenzListener next)
        let damage = incommingInstance.getDamage()
        if damage <= 0
            next.continue()
            return
        AnimationWait.time(entity.getBoard(), 0.2) ->
            let transferDmg = damage * (transferPerc * reducePerc)
            incommingInstance.setDamage((incommingInstance.getOverKillDamage() * transferPerc).round())
            
            let preview = DamagePreview.create(incommingInstance.getSourceType(), incommingInstance.getType())
            preview.addFlag(DamageFlags.TRIGGERED)
            preview.addFlag(DamageFlags.UNMITIGATABLE)
            preview.dmg = realRange(transferDmg, transferDmg)
            let linkDamageSpecfic = preview.getRandomInstance()
                
            BoardController.dealDamage(entity, linkDamageSpecfic, primary.entity) ->
                destroy linkDamageSpecfic
                destroy preview
                AnimationWait.time(entity.getBoard(), 0.2) ->
                    next.continue()

    override function onRefresh(StatusData data)
        if not canApplyTo(entity, data)
            return
        // refresh duration
        duration = data.duration
        applySecondaryInner(data)

    override function canPurge() returns boolean
        return false

    override function createTooltip() returns Tooltip
        return createTooltip(this)

    override function removeVisuals()
        if display != null
            display.destr()

    ondestroy
        if entity.isAlive()
            let triggers = entity.getTriggers()
            if turnStartListener != null
                triggers.removeOnTurnStart(turnStartListener)
                turnStartListener = null
            if damageTakenTrigger != null
                triggers.removeOnBeforeDamageTaken(damageTakenTrigger)
                damageTakenTrigger = null