package StatusDivineShield

import AttachmentPoints
import Abilities

import Status

import BoardEntity
import StatusType
import Tooltip
import BoardListenerOnDamage

init
    Initializer.register(InitializerNames.STATI) -> 
        StatusDivineShield.setup()

public class StatusDivineShield extends Status
    static StatusType TYPE

    static function setup()
        TYPE = new StatusType("Divine Shield", Icons.bTNDivineIntervention)
        TYPE.setCreationListener() (boardEntity, data) ->
            return new StatusDivineShield(TYPE, boardEntity, data)
        TYPE.setTooltipListener() data ->
            return createTooltipBaseLines(data)

    static constant ABSORB = 0

    private static function createTooltipBaseLines(int absorb) returns LinkedList<string>
        return new LinkedList<string>
        ..add("Absorbs a total of "
         + KStringUtils.coloredInt(KStringUtils.COLOR_POSITIVE, absorb)
         + " Damage")
        
    private static function createTooltipBaseLines(StatusData data) returns LinkedList<string>
        return createTooltipBaseLines(data.getIntData(ABSORB))

    private static function createTooltip(StatusDivineShield b) returns Tooltip
        return Tooltip.create(TYPE.getName())
        ..addLine("")
        ..addLines(createTooltipBaseLines(b.absorb))

    /* -------------------------------------------------------------------------- */

    private effect display
    private int absorb = 0
    
    private BoardListenerOnDamage damageTakenTrigger

    construct(StatusType statusType, BoardEntity entity, StatusData data)
        super(statusType, entity)
        display = entity.addAttachEffect(Abilities.divineShieldTarget, AttachmentPoints.origin)

        refreshStatusData(data)

        damageTakenTrigger = entity.listeners().addOnBeforeDamageTaken() (BoardListenerOnDamageData onDamageTakenData) ->
            let overKillDmg = onDamageTakenData.getDamage(true)
            let absorbedDmg = min(absorb, overKillDmg)
            onDamageTakenData.addDamage(-absorbedDmg)
            absorb -= absorbedDmg
            if absorb <= 0
                remove()
            onDamageTakenData.continue(false)

    override function onRefresh(StatusData data)
        refreshStatusData(data)

    override function canPurge() returns boolean
        return true

    protected function refreshStatusData(StatusData data)
        absorb += data.getIntData(ABSORB)

    override function createTooltip() returns Tooltip
        return createTooltip(this)

    override function removeVisuals()
        if display != null
            display.destr()

    ondestroy
        if entity.isAlive()
            if damageTakenTrigger != null
                entity.listeners().removeOnBeforeDamageTaken(damageTakenTrigger)
                damageTakenTrigger = null