package StatusStunned

import AttachmentPoints
import Abilities

import Status

import StatusType
import BoardEntity
import Tooltip
import InitiativeList

init
    Initializer.register(InitializerNames.STATI) -> 
        StatusStunned.setup()


public class StatusStunned extends Status
    static StatusType TYPE

    protected static function setup()
        TYPE = new StatusType("Stunned", Icons.bTNStun)
        TYPE.setCreationListener() (boardEntity, data) ->
            return new StatusStunned(TYPE, boardEntity, data)
        TYPE.setTooltipListener() data ->
            return createTooltipLines()

    static constant DURATION = 0

    private static function createTooltipLines() returns LinkedList<string>
        return new LinkedList<string>
        ..add("You are stuned!")

    /* -------------------------------------------------------------------------- */
    
    private effect display
    private InitiativeEvent initEvent

    construct(StatusType statusType, BoardEntity entity, StatusData data)
        super(statusType, entity)
        display = entity.addAttachEffect(Abilities.thunderclapTarget, AttachmentPoints.overhead)

        let duration = data.getIntData(DURATION)

        initEvent = entity.getBoard().getInitiative().addSequenzListener(duration) (SequenzListener next) ->
            remove()
            next.continue()


    override function onRefresh(StatusData data)

    override function canPurge() returns boolean
        return false

    override function createTooltip() returns Tooltip
        return TYPE.createTooltip(null)

    override function removeVisuals()
        if display != null
            display.destr()

    ondestroy
        if initEvent != null
            entity.getBoard().getInitiative().removeListener(initEvent)
            initEvent = null