package StatusController

import StatusType

import initlater Status
import initlater BoardStack
import BoardEntity

public class StatusController 

    static function has(StatusType statusType, BoardEntity entity) returns boolean
        return get(statusType, entity) != null

    static function get(StatusType statusType, BoardEntity entity) returns Status
        Status result = null
        let itr = entity.statusIterator()
        while itr.hasNext() and result == null
            let b = itr.next()
            if b.getType() == statusType
                result = b
        destroy itr
        return result

    static function add(StatusType statusType, BoardEntity entity, StatusData data)
        var b = get(statusType, entity)
        if b == null
            b = statusType.createNew(entity)
            entity.addStatus(b)
        b.update(data)

    static function hideAll(BoardStack stack)
        let itr = stack.statusIterator()
        while itr.hasNext()
            itr.next().hide()

        destroy itr
