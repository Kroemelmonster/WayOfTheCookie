package BuffController

import BuffType

import initlater Buff
import initlater BoardStack

public class BuffController 

    static function hasBuff(BuffType bType, BoardStack stack) returns boolean
        return getBuff(bType, stack) != null

    static function getBuff(BuffType bType, BoardStack stack) returns Buff
        Buff result = null
        let itr = stack.buffIterator()
        while itr.hasNext() and result == null
            let b = itr.next()
            if b.getBuffType() == bType
                result = b
        destroy itr
        return result

    static function addBuff(BuffType bType, BoardStack stack, int duration)
        var b = getBuff(bType, stack)
        if b == null
            b = bType.createNew(stack)
            b.setDuration(duration)
            stack.addBuff(b)

        b.setDuration(duration)

    
    static function proceedBuffs(BoardStack stack)
        let itr = stack.buffIterator()
        while itr.hasNext()
            let b = itr.next()

            if b.tick()
                itr.remove()

        destroy itr

    static function removeAllBuffs(BoardStack stack)
        let itr = stack.buffIterator()
        while itr.hasNext()
            let b = itr.next()

            b.kill()
            itr.remove()

        destroy itr
