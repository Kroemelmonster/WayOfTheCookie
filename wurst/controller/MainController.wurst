package MainController

import UIController
import NPC
import Stack

import initlater Hero
import initlater HostileArmy
import initlater Board
import PlayerData
import initlater UIHeroWindow
import initlater UINPCWindow
import ClosureTimers

public class MainController
    static function startFight(Hero hero, HostileArmy enemy, Board board)
        // Log.trace("HIER BEGINNT DER KAMPF ZWISCHEN "+hero.toString()+ " vs "+enemy.toString())
        hero.enable(false)
        enemy.enable(false)

        board.placeCombat(hero, enemy)
        UIController.switchToCombat(board)
        board.startCombat()


    static function endFight(Board board, int side)
        if side == -2
            Log.trace("Draw!")
        else if side == 0
            Log.trace("You Won")
        else
            Log.trace("You Lost")
        
        let hero = board.getHero(0)
        let heroArmy = hero.getArmy()
        for int i = 0 to Army.MAX_SIZE - 1
            let stack = heroArmy.getStack(i)
            if stack != null
                let looseAmount = stack.getAmount() - stack.representive.stats.getAmount()
                if looseAmount > 0
                    Log.trace("you lost "+looseAmount.toString()+" of "+stack.toString())

        let enemy = board.getHostileArmy()
        let army = enemy.getArmy()
        var exp = 0
        for int i = 0 to Army.MAX_SIZE - 1
            let stack = army.getStack(i)
            if stack != null
                let looseAmount = stack.getAmount() - stack.representive.stats.getAmount()
                exp += looseAmount * stack.getStackType().value
                if looseAmount > 0
                    Log.trace("you killed "+looseAmount.toString()+" of "+stack.toString())

        Log.trace("this would have give you a net exp of "+exp.toString() + " IF I HAD IMPLEMENTED IT!!")
        doAfter(2) ->
            UIController.switchToWorld(hero.getPlayer())
            board.endFight()
            nullTimer() ->
                hero.enable(true)
                enemy.kill()

    static function interact(Hero hero, NPC npc)
        hero.startInteract(npc)

    static function payGold(Hero hero, int amount)
        hero.addGold(-amount)

    static function addStack(Hero hero, Stack stack, int amount)
        hero.addStack(stack.getStackType(), amount)
        stack.setAmount(stack.getAmount() - amount)

    static function buyStack(Hero hero, Stack stack, int amount, int cost)
        payGold(hero, amount * cost)
        addStack(hero, stack, amount)

    static function splitStack(Hero hero, Stack stack, int intoAmount)
        if hero.getArmy().hasStack(stack)
            stack.setAmount(stack.getAmount() - intoAmount)
            hero.addStackAtFree(stack.getStackType(), intoAmount)

    static function openHeroWindow(Player p, boolean flag)
        if flag
            UINPCWindow.hide(p)
        p.hero.enable(flag == false)
        UIHeroWindow.setVisible(p, flag)
        
        
        