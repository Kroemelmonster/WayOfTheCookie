package BoardStack

import LinkedList
import Abilities
import AttachmentPoints
import ClosureEvents
import AbilityIds

import StackType
import PlayerData
import Action
import UnitDefinition
import DefaultAction
import Passive

import initlater Cell
import initlater Board
import BoardStackStats
import StackStats
import Stack
import BoardTrigger
import BoardEntity
import ActionType

public class BoardStack extends BoardEntity
    private StackType stackType
    private ArmyStack armyStack
    private Cell cell
    private Player p

    private texttag tt

    private LinkedList<Passive> passiveActions

    BoardStackStats stats

    construct(Player p, ArmyStack stack)
        super(EntityType.STACK, null)
        this.p = p
        this.passiveActions = new LinkedList<Passive>
        this.stats = new BoardStackStats(this, stack.stats, stack.getAmount())
        this.armyStack = stack
        this.stackType = stack.getStackType()
        stack.representive = this

        resetCurrentSelectedAction()

    override function getCell() returns Cell
        return cell

    override function getPlayer() returns Player
        return p

    override function getIcon() returns string
        return stackType.icon

    override function getName() returns string
        return stackType.name

    function getStackType() returns StackType
        return stackType

    function getArmyStack() returns ArmyStack
        return armyStack

    function addPassive(Passive passive)
        passiveActions.add(passive)

    @inline
    override function getMaxActionPoints() returns int
        return stats.getMaxAP()

    @inline
    override function getRemainingActionPoints() returns int
        return stats.getRemainingActionPoints()

    @inline
    // TODO mach daraus ein ovveride
    function getRemainingMoveActionPoints() returns int
        return stats.getRemainingMoveActionPoints()

    @inline
    override function removeAP(integer amount)
        stats.removeAP(amount)

    @inline
    override function resetActionPoints()
        stats.resetActionPoints()

    @inline 
    function isEnemy(BoardEntity other) returns boolean
        return p != other.getPlayer()

    @inline
    function canAttack(BoardStack other) returns boolean
        return isEnemy(other)

    function place(Cell cell)
        let playerIndex = cell.getBoard().getPlayerIndex(p)
        if playerIndex == -1
            return

        this.cell = cell
        this.board = cell.getBoard()
        createBLZUnit(playerIndex)

        if board.getInitiative().isFirstTurn() == false
            SetUnitAnimation(u, "birth")

        resetActionPoints()

        tt = createTTEx(getTextTagPos(), stats.getAmount().toString(), 10.0)


    private function createBLZUnit(int playerIndex)
        u = createUnit(DUMMY_PLAYER, stackType.unitId, cell.getCoord(), angle(PI * playerIndex))
        u.setColor(p.blzPlayer.getColor())
        placeEntity()

    function replaceCell(Cell cell)
        this.cell.removeStack()
        this.cell = cell
        this.cell.setStack(this)

    override function isAlive() returns boolean
        return isOnBoard()
    
    function isOnBoard() returns boolean
        return this.cell != null

    @inline
    override function getInitiative() returns int
        return stats.getInitiative()

    function kill()
        board.triggerOnStackKilled(this)
        removeStackRelatedThingsFromBoard()
        u.kill()

    @inline
    function isSelectByOwnPlayer() returns boolean
        return p.currentSelectedEntity == this

    function getCurrentOrder() returns int
        return u.getCurrentOrder()

    function isInRangeOf(vec2 coord, real range) returns boolean
        return u.getPos().inRange(coord, range)

    function setVisualHp(real percent)
        u.setHP(percent * 1000)

    function tick(real _delta)
        tt.setPos(getTextTagPos())
        tt.setText(stats.getAmount().toString(), 10.0)

    override function toString() returns string
        return getName() +" [" + stats.getAmount().toString() +"]"

    private function getTextTagPos() returns vec3
        var pos = u.getPos3Zero()
        pos.y -= 50
        pos.x += 33
        return pos

    private function removeStackRelatedThingsFromBoard()
        removeEntityRelatedThingsFromBoard()
        if cell != null
            cell.removeStack()
            cell = null

        if tt != null
            tt.destr()
            tt = null
    
    ondestroy
        removeStackRelatedThingsFromBoard()
        destroyEntity()

        if passiveActions != null
            for action in passiveActions
                destroy action
            destroy passiveActions
        if armyStack.isSummoned()
            destroy armyStack
        destroy stats
        destroyTriggers()
    
        u.remove()