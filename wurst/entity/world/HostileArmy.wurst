package HostileArmy


import HashMap

import Stack
import StackType
import PlayerData
import ClosureEvents
import World

constant HashMap<unit, HostileArmy> map = new HashMap<unit, HostileArmy>

public class HostileArmy
    protected Army army
    protected unit u
    protected Player p

    protected EventListener damagedListener

    construct()
        this.p = Player.NEUTRAL_AGGRESIVE
        this.army = new Army(null)

    function getPlayer() returns Player
        return p

    static function getFromUnit(unit u) returns HostileArmy
        return map.get(u)

    function createSummon(StackType stackType, int amount) returns ArmyStack
        return new ArmyStack(stackType, amount, true, null)

    function getArmy() returns Army
        return army

    function enable(boolean flag)
        if flag == false
            u.pause()
            u.setInvulnerable(true)
            u.addAbility(LOCUST_ID)
        else
            u.unpause()
            u.setInvulnerable(false)
            u.removeAbility(LOCUST_ID)

    function addStackAtFree(StackType stackType, int amount)
        army.addStackAtFree(stackType, amount)

    function addStackWithLeadershipAtFree(StackType stackType, int leaderhsip)
        army.addStackWithLeadershipAtFree(stackType, leaderhsip)

    function addStack(StackType stackType, int amount)
        army.addStack(stackType, amount)

    function toString() returns string
        return "Hostile "+army.toString()

    function placeWorld(unit chicken)
        let pos = chicken.getPos()
        chicken.remove()
        u = createUnit(p.blzPlayer, army.getFirstStack().getStackType().getArmyUnitId(), pos, angle(GetRandomReal(0, PI2)))
        map.put(u, this)

        damagedListener = EventListener.add(u, EVENT_PLAYER_UNIT_DAMAGED) ->
            if u.isInvulnerable() == false
                World.onHostileArmyDamaged(this, GetEventDamageSource())

    function kill()
        map.remove(u)
        u.unpause()
        u.kill()
        destroy this

    ondestroy
        destroy army
        destroy damagedListener

        

 