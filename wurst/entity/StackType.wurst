package StackType

import LinkedList

import StackSoundSet
import KUtil
import Talent

public enum DamageType
    PHYISICAL
    MAGIC
    FIRE
    POISON

public class StackType
    integer unitId
    integer armyUnitId
    StackSoundSet soundSet
    string icon
    string name

    integer level
    integer leadership
    integer value

    integer initiative
    integer actionPoints

    integer maxHp
    intRange damage
    intRange attackRange
    int attack
    int defence

    DamageType damageType = PHYISICAL

    int phyisicalResistance = 0
    int magicResistance = 0
    int fireResistance = 0
    int poisonResistance = 0

    LinkedList<Talent> talents = new LinkedList<Talent>()

    construct(integer unitId, integer armyUnitId, string icon, StackSoundSet soundSet, string name, int level, int leadership, int value)
        this.unitId = unitId
        this.armyUnitId = armyUnitId
        this.soundSet = soundSet
        this.icon = icon
        this.name = name
        this.level = level
        this.leadership = leadership
        this.value = value

    function addTalent(Talent talent)
        talents.add(talent)
        talent.onStackTypeCreation(this)

    @inline
    function getNonHiddenTalent(int index) returns Talent
        Talent found = null
        var counter = 0
        let itr = talents.iterator()
        while itr.hasNext() and found == null
            let talent = itr.next()
            if talent.isHidden() == false
                if index == counter
                    found = talent
                counter++

        destroy itr
        return found

    @inline
    function talentIterator() returns LLIterator<Talent>
        return talents.iterator()

    @inline
    function getResistance(DamageType dType) returns int
        switch dType
            case MAGIC
                return magicResistance
            case FIRE
                return fireResistance
            case POISON
                return poisonResistance
            default
                return phyisicalResistance

    @inline
    function setMaxHp(int value)
        maxHp = value
    
    @inline
    function setDamage(intRange value)
        damage = value

    @inline
    function getCost() returns int
        return value

    @inline
    function setAttackRange(intRange value)
        attackRange = value

    @inline
    function setInitiative(int value)
        initiative = value

    @inline
    function setActionPoints(int value)
        actionPoints = value

    @inline
    function setAttack(int value)
        attack = value

    @inline
    function setDefence(int value)
        defence = value
    
    @inline
    function setDamageType(DamageType value)
        this.damageType = value