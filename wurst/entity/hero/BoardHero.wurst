package BoardHero

import Hero
import PlayerData
import BoardEntity
import Action
import Cell
import initlater Board
import initlater HeroActionAttack
import initlater HeroActionSkip
import initlater BoardHeroStats
import HeroType
import initlater SpellAction
import ActionType
import Spell
import initlater BoardForce

public class BoardHero extends BoardEntity
    private static int MAX_QUICKCAST_SPELLS = 4
    Hero hero

    BoardHeroStats stats
    private HeroType heroType

    protected LinkedList<SpellAction> quickCastSpells = new LinkedList<SpellAction>
    
    construct(Hero hero, vec2 coord, BoardForce boardForce)
        super(EntityType.HERO, boardForce)
        this.hero = hero
        this.stats = new BoardHeroStats(this, hero.stats)
        this.heroType = hero.getType()

        createBLZUnit(coord)

        addAction(HeroActionSkip.TYPE.create(this))
        addAction(HeroActionAttack.TYPE.create(this))

        hero.forEachSpell() spell ->
            addAction(spell.create(this))

        resetAP()
        resetCurrentSelectedAction()

    private function createBLZUnit(vec2 coord)
        u = createUnit(DUMMY_PLAYER, heroType.unitId, coord , boardForce.getSide().toAngle())
        ..setColor(hero.getPlayer().blzPlayer.getColor())

        placeEntity()

    override function getCell() returns Cell
        return null

    override function getPlayer() returns Player
        return hero.getPlayer()

    override function getIcon() returns string
        return hero.getType().icon

    override function getName() returns string
        return hero.getType().name

    override protected function hasPath() returns boolean
        return false

    override function getInitiative() returns int
        return stats.getInitiative()

    override function getMaxAP() returns int
        return stats.getMaxActionPoints()

    override function getRemainingAP() returns int
        return stats.getRemainingActionPoints()

    override function getRemainingMoveAP() returns int
        return getRemainingAP()

    override function removeAP(integer amount)
        stats.removeAP(amount)

    function getMana() returns int
        return stats.mana

    function payMana(int amount)
        stats.payMana(amount)

    function addQuickCast(SpellAction action)
        if quickCastSpells.has(action)
            return
        if quickCastSpells.size() >= MAX_QUICKCAST_SPELLS
            removeQuickCast(quickCastSpells.getFirst())
        quickCastSpells.add(action)

    function removeQuickCast(SpellAction action)
        if quickCastSpells.has(action)
            quickCastSpells.remove(action)

    function isQuickCast(SpellAction action) returns boolean
        return quickCastSpells.has(action)

    override function resetAP()
        stats.resetActionPoints()

    override function isAlive() returns boolean
        return true

    override function toString() returns string
        return "BoardHero [ level : "+hero.getLevel().toString()+"]"

    ondestroy
        destroyEntity()
        removeEntityRelatedThingsFromBoard()
        
        destroyTriggers()
        u.remove()

        destroy quickCastSpells
