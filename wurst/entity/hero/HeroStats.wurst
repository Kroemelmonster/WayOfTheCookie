package HeroStats

import KUtil
import StatsModifier

import initlater Hero
import initlater HeroType
import Globals
import StackType

public class HeroStats
    private Hero hero
    HeroType heroType

    protected int mana = 0

    int basicAttack
    int basicDefence
    int basicPower
    int basicWisdom

    DamageType damageType

    StatsModifier attackModi = new StatsModifier()
    StatsModifier defenceModi = new StatsModifier()
    StatsModifier powerModi = new StatsModifier()
    StatsModifier wisdomModi = new StatsModifier()

    StatsModifier damageModi = new StatsModifier()
    protected StatsModifier realAttackModi = new StatsModifier()
    StatsModifier initModi = new StatsModifier()
    StatsModifier actionPointModi = new StatsModifier()
    StatsModifier additionalManaModi = new StatsModifier()

    construct(Hero hero, HeroType heroType)
        this.hero = hero
        this.heroType = heroType
        basicAttack = heroType.attack
        basicDefence = heroType.defence
        basicPower = heroType.power
        basicWisdom = heroType.wisdom
        damageType = heroType.damageType
        
        this.mana = getMaxMana()

    @inline
    function getInitiative() returns int
        return initModi.get(heroType.initiative).round()

    @inline
    function getMaxActionPoints() returns int
        return actionPointModi.get(heroType.actionPoints).round()

    @inline
    function getAttack() returns int
        return attackModi.get(basicAttack).round()

    @inline
    function getDefence() returns int
        return defenceModi.get(basicDefence).round()

    @inline
    function getPower() returns int
        return defenceModi.get(basicPower).round()

    @inline
    function getWisdom() returns int
        return wisdomModi.get(basicWisdom).round()

    @inline
    function getAdditionalMana() returns int
        return additionalManaModi.get(0).round()
    
    @inline
    function getMaxMana() returns int
        return getWisdom() * MANA_PER_WISDOWM + getAdditionalMana()

    function getDamage() returns realRange
        realAttackModi.addBonus = damageModi.addBonus
        realAttackModi.multi = damageModi.multi + (getAttack() * ATTACK_DAMAGE_PER_ATTACK).toInt()
        return realAttackModi.getRealRange(heroType.getDamage(hero.getLevel()), 1)


    @inline
    function getMana() returns int
        return mana

    ondestroy
        Log.error("tried to destroy herostats ???")