package HeroData

import initlater Hero
import Tooltip
import MainController

public class HeroData
    protected int might
    protected int mind
    protected int magic
    protected int gold
    protected int leadership
    protected int exp

    construct(int might, int mind, int magic, int gold, int leadership, int exp)
        this.might = might
        this.mind = mind
        this.magic = magic
        this.gold = gold
        this.leadership = leadership
        this.exp = exp

    function getMight() returns int
        return might
    
    function getMind() returns int
        return mind

    function getMagic() returns int
        return magic

    function getGold() returns int
        return gold

    function getLeadership() returns int
        return leadership

    function getExp() returns int
        return exp

public class HeroDataChange extends HeroData
    static function createForRune(int might, int mind, int magic) returns HeroDataChange
        return new HeroDataChange(might, mind, magic, 0, 0, 0)

    static function createForGold(int gold) returns HeroDataChange
        return new HeroDataChange(0, 0, 0, gold, 0, 0)

    static function pay(int gold, Hero h, int amount)
        let temp = createForGold(gold)
        temp.pay(h, amount)

    static constant string ICON_GOLD = "UI/Console/IconGold.dds"
    static constant string ICON_MIGHT = "UI/Console/IconMight.dds"
    static constant string ICON_MIND = "UI/Console/IconMind.dds"
    static constant string ICON_MAGIC = "UI/Console/IconMagic.dds"
    static constant string ICON_LEADERSHIP = "UI/Console/IconLeadership.dds"

    construct(int might, int mind, int magic, int gold, int leadership, int exp)
        super(might, mind, magic, gold, leadership, exp)

    function canPay(Hero h, int amount) returns boolean
        let data = h.data
        if data.might < might * amount
            return false
        if data.mind < mind * amount
            return false
        if data.magic < magic * amount
            return false
        if data.gold < gold * amount
            return false
        return true

    function canPay(Hero h) returns boolean
        return canPay(h, 1)
    
    function pay(Hero h, int amount)
        let data = h.data
        data.might -= might * amount
        data.mind -= mind * amount
        data.magic -= magic * amount
        data.gold -= gold * amount

        MainController.updateHeroData(h, this)

    function pay(Hero h)
        pay(h, 1)
    
    private function addIconToTooltipCost(Tooltip tt, int has, int cost, string icon)
        if cost <= 0
            return
        if has >= cost
            tt.addIcon(KStringUtils.primary(cost), icon)
        else
            tt.addIcon(KStringUtils.colored(KStringUtils.COLOR_NEGATIVE_TEXT, cost), icon)

    private function addIconToTooltipCost(Tooltip tt, int cost, string icon)
        addIconToTooltipCost(tt, INT_MAX, cost, icon)

    function setupTooltipCost(Tooltip tt, Hero h)
        let data = h.data
        addIconToTooltipCost(tt, data.might, might, ICON_MIGHT)
        addIconToTooltipCost(tt, data.mind, mind, ICON_MIND)
        addIconToTooltipCost(tt, data.magic, magic, ICON_MAGIC)
        addIconToTooltipCost(tt, data.gold, gold, ICON_GOLD)
        // TODO this should be based on stackType
        addIconToTooltipCost(tt, data.leadership, leadership, ICON_LEADERSHIP)

    function setupTooltipCost(Tooltip tt)
        addIconToTooltipCost(tt, might, ICON_MIGHT)
        addIconToTooltipCost(tt, mind, ICON_MIND)
        addIconToTooltipCost(tt, magic, ICON_MAGIC)
        addIconToTooltipCost(tt, gold, ICON_GOLD)
        // TODO this should be based on stackType
        addIconToTooltipCost(tt, leadership, ICON_LEADERSHIP)