package WarcryBuff

import AttachmentPoints
import Icons
import Abilities

import Buff
import UITooltip

import initlater BoardStack
import initlater BuffType
import BoardTrigger

public class WarcryBuff extends Buff
    private static constant DAMAGE_MULTI = 10
    private static constant string NAME = "Warcry"

    private effect display
    private int duration

    private BoardTrigger turnEndListener

    static function setup()
        BuffTypeSet.WARCRY = new BuffType(Icons.bTNBattleRoar, NAME, createTooltip(null)) (BoardStack stack) ->
            return new WarcryBuff(BuffTypeSet.WARCRY, stack)

    construct(BuffType bType, BoardStack stack)
        super(bType, stack)
        display = stack.addAttachEffect(Abilities.roarTarget, AttachmentPoints.overhead)
        
        turnEndListener = stack.getTriggers().addTurnEnd() (SequenzListener next) ->
            duration--
            updateInner(-1)
            if duration <= 0
                kill()
            next.continue()

    private static function createTooltip(WarcryBuff b) returns Tooltip
        let tooltip = Tooltip.create(NAME)
        if b == null
            tooltip.addLine("Increases the damage dealt by "+Tooltip.coloredPercent(DAMAGE_MULTI / 100.0) +" per reamining duration")
        else
            tooltip.addLine(Tooltip.stat("Duration", b.duration))
            tooltip.addLine("")
            tooltip.addLine("Increases the damage dealt by "+Tooltip.coloredPercent((DAMAGE_MULTI * b.duration) / 100.0))
        
        return tooltip

    override protected function updateInner(BuffData data)
        if this.duration < data.duration
            let difference = data.duration - this.duration
            this.duration = data.duration
            updateInner(difference)

    override function createCurrentTooltip() returns Tooltip
        return createTooltip(this)

    private function updateInner(int durationChange)
        stack.stats.damageModi.multi += (durationChange * DAMAGE_MULTI)

    override function hide()
        if display != null
            display.destr()

    ondestroy
        updateInner(duration * -1)
        stack.getTriggers().removeOnTurnEnd(turnEndListener)
        if display != null
            display.destr()