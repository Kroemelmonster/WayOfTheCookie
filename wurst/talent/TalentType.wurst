package TalentType
import Icon
import Tooltip
import BoardEntity
import Talent
import StackType
import initlater ActionAttackMove
import initlater ActionDefend
import initlater ActionWait

interface TalentCreateListener
    function create(BoardEntity boardEntity) returns Talent

interface TalentGeneratesTooltipListener
    function createTooltip(HasEntityStats stats) returns Tooltip

public interface TalentStats extends TalentGeneratesTooltipListener, HasIcon

public class TalentType extends Icon implements TalentStats

    static function addStackDefaultAction(StackType stackType)
        stackType.addTalent(ActionWait.TYPE.createTalent())
        stackType.addTalent(ActionDefend.TYPE.createTalent())
        stackType.addTalent(ActionAttackMove.TYPE.createTalent())

    protected TalentCreateListener listener
    protected TalentGeneratesTooltipListener tooltipListener

    construct(Icon icon)
        super(icon.name, icon.icon)

    construct(string name, string icon)
        super(name, icon)
    
    function onCreate(TalentCreateListener listener)
        this.listener = listener

    function onCreateTooltip(TalentGeneratesTooltipListener tooltipListener)
        this.tooltipListener = tooltipListener

    function create(BoardEntity boardEntity) returns Talent
        return listener.create(boardEntity)

    function toString() returns string
        return "TalentType '"+name+"'"

    function isHidden() returns boolean
        return getIcon() == ""

    override function getName() returns string
        return super.getName()

    override function getIcon() returns string
        return super.getIcon()

    protected function createBasicTooltip() returns Tooltip
        return Tooltip.create(getName())

    override function createTooltip(HasEntityStats stats) returns Tooltip
        return tooltipListener.createTooltip(stats)